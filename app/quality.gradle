apply plugin: 'checkstyle'

checkstyle {
    toolVersion = "6.15"
}

repositories {
    mavenCentral()
}

configurations {
    ktlint
}

dependencies {
    ktlint('com.pinterest:ktlint:0.45.2') {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    def outputFile = "${buildDir}/reports/checkstyle/ktlint.xml"
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    args "src/**/*.kt"
    args "--reporter=plain", "--reporter=checkstyle,output="+outputFile
    jvmArgs "--add-opens", "java.base/java.lang=ALL-UNNAMED"
    ignoreExitValue true
    doLast {
        def exitValue = executionResult.get().exitValue
        if(exitValue == 0) {
            print "ktlint finished successfully"
        } else {
            throw new GradleException('Fix style for kotlin code, see report: ' + outputFile)
        }
    }
}

check.dependsOn 'checkstyle','ktlint'

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    jvmArgs "--add-opens", "java.base/java.lang=ALL-UNNAMED"
    mainClass.set("com.pinterest.ktlint.Main")
    args "-F", "src/**/*.kt"
    ignoreExitValue true
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/script/style/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}
